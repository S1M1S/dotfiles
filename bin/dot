#!/bin/zsh
DF=~/dotfiles

CONF_DIR=~/.config
DF_CONF_DIR=$DF/.config

folder_link() {
  SRC=$DF/$1
  DEST=$HOME/$1

  rm -rf $DEST &>/dev/null
  ln -s $SRC $DEST
}

config_link() {
  SRC=$DF_CONF_DIR/$1
  DEST=$CONF_DIR/$1

  rm -r $DEST &>/dev/null
  ln -s $SRC $DEST
}

link_all() {
  echo 'Making symlinks...'
  if ! [[ -e ~/.config ]]; then
    mkdir ~/.config
  fi

  links=(.zshrc .tmux.conf .hyper.js .fdignore)
  for l in ${links[@]}; do
    folder_link $l
  done

  links=(fontconfig gammastep goneovim kitty nvim sway waybar)
  for l in ${links[@]}; do
    config_link $l
  done
}

remove_dangling() {
  echo 'Removing dangling symlinks...'
  symlinks="$(fd --full-path -a -d 1 -t l ~)"
  a=("${(f)symlinks}")
  for s in ${a[@]}; do
    if ! [[ -e $s ]]; then
      rm $s
    fi
  done
}

up_repo() {
  git -C $DF pull
  . ali refresh
  echo 'Setting global gitignore...'
  git config --global core.excludesfile $DF/.gitignore_global
  link_all
  remove_dangling
}

up_system() {
  if command -v pkg &>/dev/null; then
    pkg upgrade -y
  elif command -v apt-get &>/dev/null; then
    sudo apt-get update && sudo apt-get upgrade --assume-yes
  fi

  if command -v yay &>/dev/null; then
    informant read && yay -Syu --answerclean no --answerdiff no --answeredit no --answerupgrade yes --noconfirm
  fi

  if command -v brew &>/dev/null; then
    brew update && brew upgrade && brew cleanup
  fi
}

up_all() {
  setopt LOCAL_OPTIONS NO_MONITOR
  up_repo &
  up_system &
  wait
}

if [[ $1 == link ]]; then
  link_all
elif [[ $1 == get ]]; then
  . dot_get $2
elif [[ $1 == update || $1 == up ]]; then
  up_all
  NO_REFRESH=1
elif [[ $1 == clean ]]; then
  remove_dangling
  NO_REFRESH=1
else
  # dot
  echo "USAGE: "
  echo "dot link    -> make symlinks in home directory"
  echo "dot update  -> run git pull on the dotfile directory"

  NO_REFRESH=1
fi

if ! [[ $NO_REFRESH = 1 ]]; then
  . ~/dotfiles/bin/ali refresh
fi

return 0
